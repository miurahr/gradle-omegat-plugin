plugins {
    id "groovy"
    id "signing"
    id 'groovy-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'maven-publish'
}

group = 'org.omegat'
version = currentVersion
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

file("$rootDir/src/main/resources/org/omegat/gradle/omegat.properties").withReader {
    Properties props = new Properties()
    props.load(it)
    project.ext.omegatRepositoryUrl = props.getProperty("mavenRepositoryUrl")
}

gradlePlugin {
    plugins {
        omegatPlugin {
            id = 'org.omegat.gradle'
            implementationClass = 'org.omegat.gradle.OmegatPlugin'
        }
    }
}

pluginBundle {
    vcsUrl = githubUrl
    website = projectUrl
    description = projectDesc
    tags = ['OmegaT']
    plugins {
        omegatPlugin {
            displayName = 'OmegaT plugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
}

// The Gradle plugin portal doesn't allow signature files.
if (!gradle.startParameter.taskNames.intersect(['publishPlugins'])) {
    signing {
        useGpgCmd()
        required { isReleaseVersion }
        sign configurations.archives
    }
}
